plugins {
  kotlin("multiplatform")
  id("com.android.library")
  id("org.jetbrains.kotlin.plugin.compose")
  id("org.jetbrains.compose")
}

android {
  namespace = "com.twitpane.public_resource"

  compileSdk = rootProject.extra["compileSdkVersion"] as Int
  buildToolsVersion = rootProject.extra["buildToolsVersion"] as String

  defaultConfig {
    minSdk = rootProject.extra["minSdkVersion"] as Int
  }
}

kotlin {

  androidTarget {
    kotlin {
      jvmToolchain(11)
    }
  }

  listOf(
    iosX64(),
    iosArm64(),
    iosSimulatorArm64()
  ).forEach { iosTarget ->
    iosTarget.binaries.framework {
      baseName = "publicResourcesKit"
    }
  }

  sourceSets {
    commonMain {
      dependencies {
        implementation(compose.runtime)
        implementation(compose.components.resources)
      }
    }
  }
}

compose.resources {
  publicResClass = true
  packageOfResClass = "twitpane.public_resources.resources"
  generateResClass = always
}

//--------------------------------------------------
// æ–‡å­—åˆ—ãƒªã‚½ãƒ¼ã‚¹ã®åŒæœŸã‚¿ã‚¹ã‚¯
//--------------------------------------------------

// TODO shared/domain ã‚‚å«ã‚ã¦ä¸‹è¨˜ã‚’å…±é€šåŒ–ã™ã‚‹ã“ã¨

// Compose Resource ã§å®šç¾©ã—ãŸæ–‡å­—åˆ—ãƒ•ã‚¡ã‚¤ãƒ«ã‚’ Android ã® res/values/strings.xml ã«åŒæœŸã™ã‚‹ã‚¿ã‚¹ã‚¯
// â€»Android ã®ãƒ©ã‚¤ãƒ–ãƒ©ãƒªãƒ¢ã‚¸ãƒ¥ãƒ¼ãƒ«ã‹ã‚‰ R.string ã§å‚ç…§ã§ãã‚‹ã‚ˆã†ã«ã™ã‚‹ãŸã‚
val syncStringsToAndroidRes by tasks.registering {
  val sourceDir = file("src/commonMain/composeResources")
  val targetDir = file("src/androidMain/res")

  outputs.dir(targetDir)

  doLast {
    println("ðŸ”„ Syncing Compose Resource strings.xml (all locales) to Android res/")

    sourceDir.walkTopDown().forEach { file ->
      // å¯¾è±¡: src/commonMain/composeResources/values[-xx]/strings.xml
      if (file.isFile && file.name == "strings.xml" && file.parentFile.name.startsWith("values")) {
        val relativePath = file.relativeTo(sourceDir) // e.g., values-ja/strings.xml
        val targetFile = targetDir.resolve(relativePath)

        println("ðŸ“„ Copying ${file.path} â†’ ${targetFile.path}")
        targetFile.parentFile.mkdirs()
        targetFile.writeText(file.readText())
      }
    }
  }
}
afterEvaluate {
  val tasksNeedingRes = listOf(
    "compileDebugKotlinAndroid",
    "compileReleaseKotlinAndroid",
    "extractDeepLinksDebug",
    "extractDeepLinksRelease",
    "generateComposeResClass",
    "generateDebugRFile",
    "generateDebugResources",
    "generateReleaseRFile",
    "generateReleaseResources",
    "extractDeepLinksForAarRelease",
  )

  tasksNeedingRes.forEach { name ->
    tasks.named(name).configure {
      dependsOn(syncStringsToAndroidRes)
    }
  }
}
